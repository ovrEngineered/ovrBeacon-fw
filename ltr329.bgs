#
# @author Christopher Armenio
#


######## private constants ########


######## public variables ########
export dim LTR329_ERROR


######## private variables ########
dim result
dim data_len
dim data(1)

dim lightVal_255


######## public procedures ########
export procedure ltr329_init()
   # verify that the sensor is present
   call hardware_i2c_write($52, 0, 1, "\x87")
   call hardware_i2c_read($52, 1, 1)(result, data_len, data(:))
   if( (result = 0) && (data(0:1) = $05) ) then
      LTR329_ERROR = 0
   else
      LTR329_ERROR = 1
   end if

   # enable or disable as requested
   if( PS_IS_LIGHT_ENABLED ) then
      # bring out of standby, set gain
      call hardware_i2c_write($52, 1, 2, "\x80\x0D")
   else
      # put into standby mode
      call hardware_i2c_write($52, 1, 2, "\x80\x00")
   end if

   # set some initial values
   lightVal_255 = 0
   call attributes_write(char_lightSnsEnabled, 0, 1, PS_IS_LIGHT_ENABLED)
   call attributes_write(char_lightValue, 0, 1, lightVal_255)
   call ovrBeaconAdvertManager_updateLight(lightVal_255)
   call ovrBeaconAdvertManager_updateLightEnabled(PS_IS_LIGHT_ENABLED)
end


export procedure ltr329_setEnabled(isEnabledIn)
   call persistentStore_setLightEnabled(isEnabledIn)
   call ltr329_init()
end


export procedure ltr329_update()
   if( PS_IS_LIGHT_ENABLED ) then
      # read channel 1 (infra-red)
      call hardware_i2c_write($52, 0, 1, "\x88")
      call hardware_i2c_read($52, 1, 1)(result, data_len, data(:))
      call hardware_i2c_write($52, 0, 1, "\x89")
      call hardware_i2c_read($52, 1, 1)(result, data_len, data(:))

      # read channel 2 (visible)
      call hardware_i2c_write($52, 0, 1, "\x8A")
      call hardware_i2c_read($52, 1, 1)(result, data_len, data(:))
      lightVal_255 = data(0:1)
      # we don't care about the lower byte...but should read it anyways
      call hardware_i2c_write($52, 0, 1, "\x8B")
      call hardware_i2c_read($52, 1, 1)(result, data_len, data(:))

      # check the end-result...if it wasn't successful, the other ones
      # probabably weren't either
      if( result = 0 ) then
         LTR329_ERROR = 0
      else
         LTR329_ERROR = 1
      end if

      # update our GATT attributs and advertising value
      call attributes_write(char_lightValue, 0, 1, lightVal_255)
      call ovrBeaconAdvertManager_updateLight(lightVal_255)
      call ovrBeaconAdvertManager_updateLightEnabled(PS_IS_LIGHT_ENABLED)
   end if
end
